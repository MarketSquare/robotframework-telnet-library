*** Setting ***
#Test Setup        Open Connection    ${HOST}
Test Teardown     Close All Connections
Library           Telnet    3.142    CRLF    $    REGEXP    ASCII    strict    DeBuG
Library           String
Resource          telnet_resource.txt

*** Test Cases ***

Terminal Emulation
    Login and set prompt    terminal_emulation=True
    ${output}=    Execute Command    echo -e "abba\\x1b[3Dcdc"
    Should Match   ${output}    acdc\r\n*

Terminal Emulation Read Until Regex
    Login and set prompt    terminal_emulation=True
    Write    echo -e "abba\\x1b[3Dcdc"
    Read Until Regexp    acdc

Terminal Emulation Read Until Multiple Regexp
    Login and set prompt    terminal_emulation=True
    Write    echo -e "abba\\x1b[3Dcdc"
    Read Until Regexp    foo    acdc    bar

Terminal Emulation Reads Only the Necessary Amount
    Login and set prompt    terminal_emulation=True
    Write    echo -e "abba\\x1b[3Dcdc_foo_bar_dar"
    Read Until Regexp Should match    acdc    acdc
    Read Until should match    _foo      foo
    Read Until should match     _bar      bar
    Read Until regexp should match   _dar    dar

Terminal Emulation Reads Only the Necessary Amount with rewrites
    Login and set prompt    terminal_emulation=True
    Write    echo -e "abba\\x1b[3Dcdc_foo_"
    Read Until Regexp Should match    acdc    acdc
    Write Bare    echo -e "abba\\x1b[3Ddhd_bar_"\r\n
    Read Until Regexp Should match    _foo_*adhd    adhd
    Write Bare    echo -e "abba\\x1b[3Dmma_more"\r\n
    Read until Should Match    _bar_*amma     amma
    Read Until Should match    _more          more

Terminal Emulation empties buffer on not found read until
    Login and set prompt    terminal_emulation=True
    Write    echo abba
    Run keyword and expect error    No match found *      Read until    not there
    Write bare      token
    Read until should match    token     token

Terminal Emulation empties buffer on not found read until regexp
    Login and set prompt    terminal_emulation=True
    Write    echo abba
    Run keyword and expect error    No match found *      Read until regexp    not there
    Write bare      token
    Read until regexp should match    token     token

Terminal Emulation read
    Login and set prompt    terminal_emulation=True
    Write    echo -e "abba\\x1b[3Dcdc"
    Read Should Match     acdc*
    Write bare      token
    Read should match    token


*** Keywords ***
Read until should match
    [Arguments]    ${expected}   ${match}
    ${output}=      Read until   ${match}
    Should match    ${output}    ${expected}

Read until regexp should match
    [Arguments]    ${expected}     @{match}
    ${output}=      Read until regexp   @{match}
    Should match    ${output}    ${expected}

Read Should Match
    [Arguments]    ${expected}
    ${output}=      Read
    Should match    ${output}    ${expected}