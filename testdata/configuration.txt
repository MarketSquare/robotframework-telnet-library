*** Setting ***
Test Setup        Open Connection    ${HOST}
Test Teardown     Close All Connections
Library           Telnet    123    CRLF    $    REGEXP    ASCII    DeBuG
Resource          telnet_resource.txt


*** Test Case ***

Prompt Set In Init
    Prompt Should Be    $    ${TRUE}

Prompt Set In Open Connection
    Open Connection    ${HOST}    prompt=xxx
    Prompt Should Be    xxx    ${FALSE}
    Open Connection    ${HOST}    ${EMPTY}    ${EMPTY}    ${EMPTY}    ${EMPTY}
    ...    prompt    regexp
    Prompt Should Be    prompt    ${TRUE}

Set Prompt Keyword
    Set Prompt    >-<
    Prompt Should Be    >-<    ${FALSE}
    Set Prompt    >\\s+    regexp
    Prompt Should Be    >\\s+    ${TRUE}

Timeout Set In Init
    Timeout Should Be    2 minutes 3 seconds

Timeout Set In Open Connection
    Open Connection    ${HOST}    timeout=0.5s
    Timeout Should Be    500 milliseconds
    Open Connection    ${HOST}    ${EMPTY}    ${EMPTY}    61
    Timeout Should Be    1 minute 1 second

Set Timeout Keyword
    [Documentation]    FAIL No match found for 'Not found' in 42 milliseconds
    Set Timeout    1 h 2 min 3 secs
    Timeout Should Be    1 hour 2 minutes 3 seconds
    Set Timeout    0.042
    Read Until    Not found

Newline Set In Init
    Newline Should Be    \r\n

Newline Set In Open Connection
    Open Connection    ${HOST}    newline=LF
    Newline Should Be    \n
    Open Connection    ${HOST}    ${EMPTY}    ${EMPTY}    ${EMPTY}    CR
    Newline Should Be    \r

Set Newline Keyword
    Set Newline    LF\rFOO
    Newline Should Be    \n\rFOO

Encoding Set In Init
    Encoding Should Be    ASCII

Encoding Set In Open Connection
    Open Connection    ${HOST}    encoding=ISO-8859-15
    Encoding Should Be    ISO-8859-15
    Open Connection    ${HOST}    encoding=Latin1
    Encoding Should Be    Latin1

Set Encoding Keyword
    Set Encoding    us-ascii
    Encoding Should Be    us-ascii

Default Log Level In Init
    Default Log Level Should Be    DEBUG

Default Log Level In Open Connection
    Open Connection    ${HOST}    default_log_level=trace
    Default Log Level Should Be    TRACE

Set Default Log Level Keyword
    [Documentation]    FAIL Invalid log level 'Invalid'
    [Setup]    Login And Set Prompt
    Set Default Log Level    debug
    Default Log Level Should Be    DEBUG
    Write    pwd
    Set Default Log Level    WARN
    Default Log Level Should Be    WARN
    Read Until Prompt
    Set Default log Level    Invalid

Configuration fails if there is no connection
    [Setup]    NONE
    [Template]    Should fail because no connection
    Set Prompt    $
    Set Timeout    1s
    Set Newline    LF
    Set Encoding    ASCII
    Set Default Log Level    DEBUG


*** Keywords ***
Prompt Should Be
    [Arguments]    ${expected prompt}    ${expected regexp}
    ${prompt}    ${regexp} =    Set Prompt    prompt
    Should Be Equal    ${prompt}    ${expected prompt}
    Should Be Equal    ${regexp}    ${expected regexp}
    ${prompt}    ${regexp} =    Set Prompt    ${prompt}    ${regexp}
    Should Be Equal    ${prompt}    prompt
    Should Be Equal    ${regexp}    ${FALSE}

Timeout Should Be
    [Arguments]    ${expected}
    ${timeout} =    Set Timeout    61.5
    Should Be Equal    ${timeout}    ${expected}
    ${timeout} =    Set Timeout    ${timeout}
    Should Be Equal    ${timeout}    1 minute 1 second 500 milliseconds

Newline Should Be
    [Arguments]    ${expected}
    ${newline} =    Set Newline    CRLF
    Should Be Equal    ${newline}    ${expected}
    ${newline} =    Set Newline    ${newline}
    Should Be Equal    ${newline}    \r\n

Encoding Should Be
    [Arguments]    ${expected}
    ${encoding} =    Set Encoding    ASCII
    Should Be Equal    ${encoding}    ${expected}
    ${encoding} =    Set Encoding    ${encoding}
    Should Be Equal    ${encoding}    ASCII

Default Log Level Should Be
    [Arguments]    ${expected}
    ${level} =    Set Default Log Level    WARN
    Should Be Equal    ${level}    ${expected}
    ${level} =    Set Default Log Level    ${level}
    Should Be Equal    ${level}    WARN
