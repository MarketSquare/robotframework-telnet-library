*** Setting ***
Suite Teardown    Close All Connections
Test Setup        Login And Set Prompt
Test Teardown     Close Connection
Library           Telnet    1 s 500 ms

*** Variable ***
${dir}            longdirname
${dirs}           ${dir}1/${dir}2/${dir}3/${dir}4/${dir}5/${dir}6

*** Test Case ***
Write & Read
    Write    pwd
    sleep    300 ms
    ${out} =    Read
    Should Be Equal    ${out}    /home/${USERNAME}\r\n${FULL PROMPT} \
    Write    pwd
    sleep    300 ms
    ${out} =    Read    warn
    Should Be Equal    ${out}    /home/${USERNAME}\r\n${FULL PROMPT} \

Write Long Commandline
    Write    mkdir -p ${dirs}
    Read Until Prompt
    Write    ls -l ${dir}1/${dir}2/${dir}3/${dir}4/${dir}5
    ${out} =    Read Until Prompt
    Should Contain    ${out}    ${dir}6
    Should End With    ${out}    ${FULL PROMPT}
    Write    rm -rf ${dir}1

Read Until
    [Documentation]    FAIL No match found for 'Not found' in 1 second 500 milliseconds
    Write    pwd
    ${out} =    Read Until    /
    Should Be Equal    ${out}    /
    ${out} =    Read Until    /    DEBUG
    Should Be Equal    ${out}    home/
    Read Until    Not found

Read Until Regexp
    [Documentation]    FAIL No match found for 'Not found' in 1 second 500 milliseconds
    Write    pwd
    ${out} =    Read Until Regexp    /h[abo]me.${USERNAME}\\s+
    Should Be Equal    ${out}    /home/${USERNAME}\r\n
    ${out} =    Read Until Regexp    .*@    deBUG
    Should Be Equal    ${out}    ${PROMPT START}
    ${out} =    Read Until Regexp    Not found

Write Until Expected Output
    [Documentation]    FAIL No match found for 'Not found' in 1 second
    [Setup]    Login and Set Timeout
    Write    a=3
    Write Until Expected Output    a=$(($a - 1)); if (($a == 0)); then echo BLAST OFF; fi\r\n    BLAST OFF    2 s    0.4s
    Write Until Expected Output    ls    Not found    1s    0.1s
    [Teardown]    Set Timeout    1.5 s

Read Until Prompt
    [Documentation]    FAIL No match found for '$$' in 1 second 500 milliseconds
    Set Prompt    $
    Write    pwd
    ${out} =    Read Until Prompt
    Should Be Equal    ${out}    /home/${USERNAME}\r\n${FULL PROMPT}
    Write    pwd
    ${out} =    Read Until Prompt    wArN
    Should Be Equal    ${out}    /home/${USERNAME}\r\n${FULL PROMPT}
    Set Prompt    $$
    Write    pwd
    Read Until Prompt

Read Until Regexp Prompt
    [Documentation]    FAIL No match found for 'No match' in 1 second 500 milliseconds
    Set Prompt    \\$    REGEXP
    Write    pwd
    ${out} =    Read Until Prompt
    Should Be Equal    ${out}    /home/${USERNAME}\r\n${FULL PROMPT}
    Set Prompt    No match    true
    Write    pwd
    Read Until Prompt

Write &
    Write    pwd &
    Write    ${PWD}
    Write    ${PWD_AMP}

Write Unicode
    [Documentation]    FAIL REGEXP: (ValueError: Only ASCII characters are allowed in Telnet. Got: .*|No match found for '.*
    ...    ' in 1 second 500 milliseconds)
    Write    ${unic_string}

Set Prompt
    ${prompt}    ${is_regexp} =    Set Prompt    $$$    REGEXP
    Should Be Equal    ${prompt}    $
    Should Be Equal    ${is_regexp}    ${False}
    ${prompt}    ${is_regexp} =    Set Prompt    $
    Should Be Equal    ${prompt}    $$$
    Should Be Equal    ${is_regexp}    ${True}

Set Timeout
    [Documentation]    FAIL No match found for 'Not found' in 500 milliseconds
    ${to} =    Set Timeout    3 min 2 secs
    Should Be Equal    ${to}    1 second 500 milliseconds
    ${to} =    Set Timeout    0.5
    Should Be Equal    ${to}    3 minutes 2 seconds
    Read Until    Not found

Set Newline
    ${newline} =    Set Newline    LF
    Should Be Equal    ${newline}    \r\n

Set Default Log Level
    [Documentation]    FAIL Invalid log level 'Invalid'
    Set Default log Level    DEBUG
    Write    pwd
    Set Default log Level    warn
    Read Until Prompt
    Set Default log Level    Invalid

Invalid Login
    [Documentation]    FAIL Login incorrect
    [Setup]
    Open Connection    ${HOST}
    Login    invalid    invalid
    [Teardown]    Close All Connections

Invalid Login With Prompt Set
    [Documentation]    FAIL Login incorrect
    [Setup]
    Open Connection    ${HOST}
    Set Prompt    $
    Login    invalid    invalid
    [Teardown]    Close All Connections

Switch Connection
    [Setup]
    ${first} =    Login And Set Prompt
    Write    cd /tmp
    Read Until Prompt
    Write    pwd
    ${dir} =    Read Until Prompt
    Should Start With    ${dir}    /tmp
    ${second} =    Login And Set Prompt
    ${old} =    Switch Connection    ${first}
    Should Be Equal    ${old}    ${second}

Execute Command
    ${output} =    Execute Command    pwd
    Should Be Equal    ${output}    /home/${USERNAME}\r\n${FULL PROMPT}

*** Keyword ***
Login And Set Prompt
    ${index} =    Open Connection    ${HOST}
    Set Prompt    $
    Login    ${USERNAME}    ${PASSWORD}
    [Return]    ${index}

Login And Set Timeout
    Login and Set Prompt
    Set Timeout    0.3
