*** Setting ***
Test Setup        Login And Set Prompt    alias=ORIGINAL
Test Teardown     Close All Connections
Library           Telnet

*** Variable ***
${PROMPT}         $${SPACE}
${DIR}            longdirname
${DIRS}           ${dir}1/${dir}2/${dir}3/${dir}4/${dir}5/${dir}6

*** Test Case ***
Write & Read
    Write    pwd
    Sleep    300 ms
    ${out} =    Read
    Should Be Equal    ${out}    ${HOME}\r\n${FULL PROMPT}
    Write    echo Hyvää yötä
    Sleep    300 ms
    ${out} =    Read    debug
    Should Be Equal    ${out}    Hyvää yötä\r\n${FULL PROMPT}

Read Until
    [Documentation]    FAIL No match found for 'Not found' in 500 milliseconds
    Write    pwd
    ${out} =    Read Until    /
    Should Be Equal    ${out}    /
    ${out} =    Read Until    /    DEBUG
    Should Be Equal    ${out}    home/
    Read
    Write    echo Hyvää yötä
    ${out} =    Read Until    yötä
    Should Be Equal    ${out}    Hyvää yötä
    Read Until    Not found

Read Until Regexp
    [Documentation]    FAIL No match found for 'Not found' in 500 milliseconds
    Write    pwd
    ${out} =    Read Until Regexp    /h[abo]me.${USERNAME}\\s+
    Should Be Equal    ${out}    ${HOME}\r\n
    ${out} =    Read Until Regexp    .*@    deBUG
    Should Be Equal    ${out}    ${PROMPT START}
    Read
    Write    echo Päivää
    ${out} =    Read Until Regexp    no match    ää   debug
    Should Be Equal    ${out}    Päivää
    Read Until Regexp    Not found

Read Until Prompt
    [Documentation]    FAIL No match found for '$$' in 500 milliseconds
    Write    pwd
    ${out} =    Read Until Prompt
    Should Be Equal    ${out}    ${HOME}\r\n${FULL PROMPT}
    Write    pwd
    ${out} =    Read Until Prompt    dEbUg
    Should Be Equal    ${out}    ${HOME}\r\n${FULL PROMPT}
    Set Prompt    $$
    Write    pwd
    Read Until Prompt

Read Until Regexp Prompt
    [Documentation]    FAIL No match found for 'No match' in 500 milliseconds
    Set Prompt    \\$\\s    REGEXP
    Write    pwd
    ${out} =    Read Until Prompt
    Should Be Equal    ${out}    ${HOME}\r\n${FULL PROMPT}
    Set Prompt    No match    true
    Write    pwd
    Read Until Prompt

Write Long Commandline
    Write    mkdir -p ${dirs}
    Read Until Prompt
    Write    ls -l ${dir}1/${dir}2/${dir}3/${dir}4/${dir}5
    ${out} =    Read Until Prompt
    Should Contain    ${out}    ${dir}6
    Should End With    ${out}    ${FULL PROMPT}
    Write    rm -rf ${dir}1

Write Until Expected Output
    [Documentation]    FAIL No match found for 'Not found' in 300 milliseconds
    Write    a=10
    Write Until Expected Output    a=$(($a - 1)); if (($a == 0)); then echo BLAST OFF; fi\r\n    BLAST OFF    2 s    10ms
    Write Until Expected Output    ls    Not found    300ms    100ms

Set Prompt
    ${old}    ${regexp} =    Set Prompt    $$$    REGEXP
    Should Be Equal    ${old}    ${PROMPT}
    Should Be Equal    ${regexp}    ${False}
    ${old}    ${regexp} =    Set Prompt    $
    Should Be Equal    ${old}    $$$
    Should Be Equal    ${regexp}    ${True}

Set Timeout
    [Documentation]    FAIL No match found for 'Not found' in 42 milliseconds
    ${to} =    Set Timeout    1 h 2 min 3 secs
    Should Be Equal    ${to}    500 milliseconds
    ${to} =    Set Timeout    0.042
    Should Be Equal    ${to}    1 hour 2 minutes 3 seconds
    Read Until    Not found

Set Newline
    ${newline} =    Set Newline    LF
    Should Be Equal    ${newline}    \r\n
    ${newline} =    Set Newline    ${newline}
    Should Be Equal    ${newline}    \n

Set Default Log Level
    [Documentation]    FAIL Invalid log level 'Invalid'
    Set Default log Level    DEBUG
    Write    pwd
    Set Default log Level    warn
    Read Until Prompt
    Set Default log Level    Invalid

Invalid Login
    [Documentation]    FAIL Login incorrect
    [Setup]    Open Connection    ${HOST}
    Login    invalid    password

Invalid Login With Prompt Set
    [Documentation]    FAIL Login incorrect
    [Setup]    Open Connection    ${HOST}    timeout=5s    prompt=${PROMPT}
    Login    ${USERNAME}    ${EMPTY}

Switch Connection
    ${index} =    Login And Set Prompt
    Write    cd /tmp
    Read Until Prompt
    Current Directory Should Be    /tmp
    Switch Connection    ORIGINAL
    Current Directory Should Be    ${HOME}
    Switch Connection    ${index}
    Current Directory Should Be    /tmp

Execute Command
    ${output} =    Execute Command    pwd
    Should Be Equal    ${output}    ${HOME}\r\n${FULL PROMPT}

*** Keyword ***
Login And Set Prompt
    [Arguments]    ${alias}=${NONE}
    ${index} =    Open Connection    ${HOST}    prompt=${PROMPT}    alias=${alias}
    Login    ${USERNAME}    ${PASSWORD}
    Set Timeout    0.5 seconds    # Must set after login to
    [Return]    ${index}

Current Directory Should Be
    [Arguments]    ${expected}
    ${dir} =    Execute Command    pwd
    Should Start With    ${dir}    ${expected}\r\n
